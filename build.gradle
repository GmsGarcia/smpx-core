plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'pt.gmsgarcia'
version = '0.0.1-PRE-ALPHA'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation("com.google.guava:guava:33.5.0-jre")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.10")
    }
    shadowJar {
        archiveClassifier.set("") // So it produces smpx-core.jar (not smpx-core-all.jar)
        relocate "com.zaxxer.hikari", "pt.gmsgarcia.smpx.core.libs.hikari"
    }

    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [name: name, version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
